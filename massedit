#!/usr/bin/env perl
# Upload a bunch of new pages
use strict;
use warnings;
use lib 'lib';
use MW::UserAgent;
use Data::Dump qw( dump );
use File::Basename qw( basename );
use Getopt::Std qw( getopts );

my %args;
getopts('enrp:qs:', \%args) or die "Bad args\n";
die "Usage: $0 files\n" unless @ARGV;
my $prefix = $args{p};
my $summary = $args{s};

my $ua = MW::UserAgent->new('http://battlenations.wikia.com/api.php');
$ua->login();

my @args;
push @args, 'createonly' unless $args{e} || $args{n};
push @args, 'nocreate' if $args{n};

foreach my $file (@ARGV) {
   print $file, "\n";
   my $text;
   {
      open my $F, '<', $file or die "Can't read $file: $!";
      local $/ = undef;
      $text = <$F>;
   }
   (my $page = $file) =~ s{^.*/}{};
   $page = $prefix . $file if $prefix;
   $page =~ s/_/ /g;
   my $info = $ua->get_info($page, 'edit');
   my $token = $info->{edittoken} or die 'no token returned';
   if ($args{q} && !exists($info->{missing})) {
      print "exists\n";
      unlink $file if $args{r};
      sleep 1;
      next;
   }
   $ua->edit($page, $text, $summary, $token, @args);
   unlink $file if $args{r};
   sleep 5;
}

