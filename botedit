use strict;
use warnings;
use lib 'lib';
use MW::UserAgent;

my $ua = MW::UserAgent->new('http://battlenations.wikia.com/api.php');
$ua->login();

my $summary = 'Update';

die "usage: $0 files\n" unless @ARGV;

foreach my $file (@ARGV) {
   my $oldfile = $file;
   $file =~ s/\.old$//i or $oldfile .= '.old';

   my ($text, $name) = read_file($file) or next;
   my ($oldtext) = read_file($oldfile) or next;
   write_file('temp.old', $summary, "\n", $oldtext);
   write_file('temp', $summary, "\n", $text);
   system 'diff -u temp.old temp > temp.diff';

   print "\n>>> $name <<<\n";
   sleep 1;
   my $info = $ua->get_info($name, 'text', 'edit') or die;
   die unless $info->{_text};
   die unless $info->{edittoken};

   write_file('temp.old', $summary, "\n", $info->{_text});
   write_file('temp', $summary, "\n", $info->{_text});
   system 'patch -p0 -N -l < temp.diff';
   next if unlink 'temp.rej';

   my ($newtext) = read_file('temp') or die;
   if (minspace($newtext) eq minspace($info->{_text})) {
      unlink $oldfile;
      next;
   }

   my $line;
   while (1) {
      system 'diff -bu temp.old temp';
      print "--- $summary\n";
      print "ok? ";
      $line = <STDIN>;
      last unless $line && $line =~ /e/i;
      system 'vi temp';
      ($newtext, $summary) = read_file('temp') or die;
   }
   last unless defined $line;
   if ($line =~ /y/i) {
      $ua->edit($name, $newtext, $summary, $info->{edittoken}, 'nocreate');
      unlink $oldfile;
      print "Edited\n";
   }
   unlink $oldfile if $line =~ /n/i;
   last if $line =~ /q/i;
}

unlink 'temp', 'temp.old', 'temp.diff', 'temp.orig';

sub minspace {
   my ($str) = @_;
   $str =~ s/[ \t]+/ /g;
   return $str;
}

sub read_file {
   my ($file) = @_;
   open my $F, '<:encoding(utf8)', $file or die "Can't read $file: $!\n";
   my $name = <$F>;
   return unless defined $name;
   chomp $name;
   local $/ = undef;
   my $text = <$F>;
   close $F;
   $text =~ s/\n__DUMP__\n.*//s;
   return ($text, $name);
}

sub write_file {
   my ($file, @text) = @_;
   open my $F, '>:encoding(utf8)', $file or die "Can't write $file: $!\n";
   print $F @text or die "print: $!";
   close $F or die "close: $!";
}

