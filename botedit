use strict;
use warnings;
use lib 'lib';
use MW::UserAgent;

my $ua = MW::UserAgent->new('http://battlenations.wikia.com/api.php');
$ua->login();

my $summary = 'Unit limit';

die "usage: $0 files\n" unless @ARGV;

foreach my $file (@ARGV) {
   my $oldfile = $file;
   $file =~ s/\.old$//i or $oldfile .= '.old';

   my ($text, $name) = read_dump_file($file);
   my ($oldtext) = read_dump_file($oldfile);
   write_file('temp.old', $oldtext);
   write_file('temp', $text);
   system 'diff -u temp.old temp > temp.diff';

   print "\n>>> $name <<<\n";
   sleep 1;
   my $info = $ua->get_info($name, 'text', 'edit') or die;
   die unless $info->{_text};
   die unless $info->{edittoken};

   write_file('temp.old', $info->{_text});
   write_file('temp', $info->{_text});
   system 'patch -p0 -N -l < temp.diff';

   my $newtext = read_file('temp');
   if (minspace($newtext) eq minspace($info->{_text})) {
      unlink $oldfile;
      next;
   }

   my $line;
   while (1) {
      system 'diff -bu temp.old temp';
      print "ok? ";
      $line = <STDIN>;
      last unless $line && $line =~ /e/i;
      system 'vi temp';
      $newtext = read_file('temp');
   }
   last unless defined $line;
   if ($line =~ /y/i) {
      $ua->edit($name, $newtext, $summary, $info->{edittoken}, 'nocreate');
      unlink $oldfile;
      print "Edited\n";
   }
   unlink $oldfile if $line =~ /n/i;
   last if $line =~ /q/i;
}

unlink 'temp', 'temp.old', 'temp.diff', 'temp.orig', 'temp.rej';

sub minspace {
   my ($str) = @_;
   $str =~ s/[ \t]+/ /g;
   return $str;
}

sub read_dump_file {
   my ($file) = @_;
   open my $F, '<:encoding(utf8)', $file or die "Can't read $file: $!\n";
   my $name = <$F>;
   die unless defined($name);
   chomp($name);
   local $/ = undef;
   my $text = <$F>;
   close $F;
   $text =~ s/\n__DUMP__\n.*//s;
   return ($text, $name);
}

sub read_file {
   my ($file) = @_;
   open my $F, '<:encoding(utf8)', $file or die "Can't read $file: $!\n";
   local $/ = undef;
   my $text = <$F>;
   close $F;
   return $text;
}

sub write_file {
   my ($file, $text) = @_;
   open my $F, '>:encoding(utf8)', $file or die "Can't write $file: $!\n";
   print $F $text or die "print: $!";
   close $F or die "close: $!";
}

