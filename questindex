#!/usr/bin/env perl
# Generate index page for missions
use strict;
use warnings;

my %groups;
foreach my $file (glob('wiki/Level*missions')) {
   print $file, "\n";
   my $page = $file;
   $page =~ s{^.*/}{};
   $page =~ s/_/ /g;
   open my $F, '<', $file or die "Can't read $file: $!\n";
   while (<$F>) {
      my ($name) = /^===\s*(.*?)\s*===$/ or next;
      my $nm = $name;
      $nm =~ s/\s*-\s*part\s+\d+$//i;
      $nm =~ s/\s+[IV]{2,}$// and $nm =~ s/\s+part$//i;
      $nm =~ s/^(?:a|an|the)\s+//i;
      $nm =~ /(\w)/ or next;
      $groups{uc($1)}{$nm} ||= "[[$page#$name|$nm]]";
   }
}

open my $F, '>', 'missions/Mission_index' or die;

foreach my $key (sort keys %groups) {
   my $group = $groups{$key};
   my @col1 = map { $group->{$_} } sort keys %$group;
   my @col2 = splice @col1, (@col1+1)/2;
   print $F "==$key==\n{{Col-begin}}\n";
   print $F "* $_\n" foreach @col1;
   print $F "{{Col-2}}";
   print $F "* $_\n" foreach @col2;
   print $F "{{Col-end}}\n\n";
}

close $F;

