use strict;
use warnings;
use lib 'lib';
use BN;
use Cairo;

my $mapname = $ARGV[0];
unless ($mapname) {
   my $landlist = BN::File->json('LandList.json');
   if (my $list = $landlist->{landList}) {
      print 'Maps: ', join(', ', @$list), "\n";
   }
   die 'no map selected';
}

my $map = BN::File->json($mapname . '.json');
my $tilefile = $map->{tileLayoutFile} or die;
my $tileset = $map->{tileSet} or die;
$tileset =~ s/\.pvr$//;
my $tiles = BN::File->json($tilefile);
$tiles = $tiles->{map} or die;
my $layer = $tiles->{layers}[0] or die;
my $data = $layer->{data} or die;
my $wid = $layer->{width} or die;
my $hgt = $layer->{height} or die;

my $Xmin = $wid;
my $Ymin = $hgt;
my $Xmax = 0;
my $Ymax = 0;
my $i = 0;
for my $y (0 .. $hgt-1) {
   for my $x (0 .. $wid-1) {
      next unless $data->[$i++];
      my $X = $x - $y;
      my $Y = $y + $y;
      $Xmin = $X if $X < $Xmin;
      $Xmax = $X if $X > $Xmax;
      $Ymin = $Y if $Y < $Ymin;
      $Ymax = $Y if $Y > $Ymax;
   }
}

my $im_wid = ($Xmax - $Xmin + 2) * 60 + 16;
my $im_hgt = ($Ymax - $Ymin + 2) * 30 + 8;

my $surface = Cairo::ImageSurface->create('argb32', $im_wid, $im_hgt);
my $ctx = Cairo::Context->create($surface);

my $tilesetfile = "../bg/${tileset}_split.png";
my $tileimg = Cairo::ImageSurface->create_from_png($tilesetfile) or die;

$i = 0;
for my $y (0 .. $hgt-1) {
   for my $x (0 .. $wid-1) {
      my $tile = $data->[$i++] or next;
      $tile--;
      my $tx = ($tile & 7) << 7;
      my $ty = ($tile >> 3) << 6;
      my $X = ($x - $y - $Xmin) * 60;
      my $Y = ($x + $y - $Ymin) * 30;
      $ctx->set_source_surface($tileimg, $X - $tx, $Y - $ty);
      $ctx->rectangle($X+2, $Y+1, 124, 62);
      $ctx->fill();
      #$ctx->set_source_rgb(1, 1, 1);
      #$ctx->move_to($X+60, $Y+30);
      #$ctx->show_text($tile);
   }
}

if (my $buildings = $map->{buildings}) {
   foreach my $info (@$buildings) {
      my $x = $wid/2 - $info->{x}/3 + 0.5;
      my $y = $hgt/2 - $info->{y}/3 - 0.5;
      $info->{X} = ($x - $y - $Xmin) * 60;
      $info->{YY} = $info->{Y} = ($x + $y - $Ymin) * 30;
      $info->{YY} += 20 if $info->{type} =~ /_egg_/; # kludge
   }
   foreach my $info (sort { $a->{YY} <=> $b->{YY} } @$buildings) {
      my $bld = BN::Building->get($info->{type}) or next;
      my $anim = BN::Animation->get($bld->animation('Default')) or next;
      $ctx->save();
      $ctx->translate($info->{X}, $info->{Y});
      $ctx->scale(-1, 1) if $info->{flip};
      $anim->render($ctx, 0);
      $ctx->restore();
   }
}

$surface->write_to_png($mapname . '.png');

