#!/usr/bin/env perl
use strict;
use warnings;
use lib 'lib';
use BN;
use File::Basename qw( basename );
use Getopt::Std qw( getopts );;
use Image::Size qw( imgsize );

my %opts;
getopts('s', \%opts) or die;
die "usage: $0 [-s] icon_name\n" unless @ARGV == 1;
my $max_size = $opts{s} ? 40 : 2048;
my $file = shift;
$file =~ s/\.png$//;
my @infiles;
foreach my $name ( BN::File->glob("$file*.png") ) {
   my $tail = substr(basename($name), length($file), 1);
   push @infiles, $name if $tail eq '.' || $tail eq '@' || $tail eq '~';
}
die "not found\n" unless @infiles;
my $infile = shift @infiles;

if (@infiles) {
   my $best_size = -s $infile;
   foreach my $file (@infiles) {
      my $size = -s $file;
      if ($opts{s} ? $size < $best_size : $size > $best_size) {
         $infile = $file;
         $best_size = $size;
      }
   }
}

my $pngcrush = "pngcrush -new -brute -rem alla";

my ($wid,$hgt) = imgsize($infile) or die;
if ($wid <= $max_size && $hgt <= $max_size) {
   system("$pngcrush $infile $file.png") == 0 or die;
}
else {
   system("pngtopam -alphapam $infile | pnmscale -xysize $max_size $max_size | pamrgbatopng > $file-temp.png") == 0 or die;
   system("$pngcrush $file-temp.png $file.png") == 0 or die;
   unlink "$file-temp.png";
}

my $old_size = -s $infile;
my $new_size = -s "$file.png";
print "$infile ($old_size bytes) => $file.png ($new_size bytes)\n";

