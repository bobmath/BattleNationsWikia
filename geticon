#!/usr/bin/env perl
use strict;
use warnings;
use Getopt::Std qw( getopts );;
use Image::Size qw( imgsize );
use File::Glob qw( bsd_glob GLOB_NOCASE );

my $dir = '/Applications/BattleNations.app/Contents/Resources/bundle';

my %opts;
getopts('s', \%opts);
die "usage: $0 [-s] icon_name\n" unless @ARGV == 1;
my $max_size = $opts{s} ? 40 : 400;
my $file = shift;
$file =~ s/\.png$//;
my @infiles = bsd_glob("$dir/$file*.png", GLOB_NOCASE) or die "not found\n";
my $infile = shift @infiles;

if (@infiles) {
   my $best_size = -s $infile;
   foreach my $file (@infiles) {
      my $size = -s $file;
      if ($opts{s} ? $size < $best_size : $size > $best_size) {
         $infile = $file;
         $best_size = $size;
      }
   }
}

my $pngcrush = "pngcrush -new -brute -rem alla";

my ($wid,$hgt) = imgsize($infile) or die;
if ($wid <= $max_size && $hgt <= $max_size) {
   system("$pngcrush $infile $file.png") == 0 or die;
}
else {
   system("pngtopam -alphapam $infile | pnmscale -xysize $max_size $max_size | pamrgbatopng > $file-temp.png") == 0 or die;
   system("$pngcrush $file-temp.png $file.png") == 0 or die;
   unlink "$file-temp.png";
}

my $old_size = -s $infile;
my $new_size = -s "$file.png";
print "$infile ($old_size bytes) => $file.png ($new_size bytes)\n";

