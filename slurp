#!/usr/bin/env perl
# Download wikitext pages from battlenations.wikia.com
use strict;
use warnings;
use LWP::UserAgent;
use XML::Parser;

my $ua = LWP::UserAgent->new();
my $first = 1;

mkdir 'wiki';

if (@ARGV) {
   get_page($_) foreach @ARGV;
   exit;
}

foreach my $dir (qw{ units buildings missions enemies }) {
   foreach my $file (glob "data/$dir/*") {
      next unless -f $file;
      open my $F, '<', $file or do { warn "Can't read $file: $!"; next };
      chomp(my $page = <$F>);
      close $F;
      get_page($page);
   }
}

sub get_page {
   my ($name) = @_;
   $name =~ s/\s+/_/g;
   my $file = $name;
   $file =~ s/[^\w\s\-.]//g;
   $file =~ s/^[-.]/_/;
   $file = "wiki/$file";
   return if -e $file;

   sleep 10 unless $first;
   $first = 0;

   print $name, "\n";
   $name =~ s/(['"])/sprintf "%%%02X", ord($1)/eg;
   my $resp = $ua->get(
      "http://battlenations.wikia.com/wiki/Special:Export/$name");
   unless ($resp->is_success()) {
      warn "$name get failed";
      return;
   }
   open my $OUT, '>:encoding(utf8)', $file
      or do { warn "Can't write $file: $!"; return };

   my $capture;
   my $start = sub {
      my ($expat, $tag) = @_;
      $capture = 1 if $tag eq 'text';
   };
   my $end = sub {
      $capture = 0 if $capture;
   };
   my $char = sub {
      my ($expat, $text) = @_;
      print $OUT $text if $capture;
   };
   my $p = XML::Parser->new(
      Handlers => { Start=>$start, End=>$end, Char=>$char });
   $p->parse($resp->content());

   close $OUT;
   warn "$name data not found" unless defined $capture;
}

